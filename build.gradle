// Check GETTING_STARTED.markdown for information regarding usage.

buildscript {
	repositories {
		maven {url "https://repository-cdn.liferay.com/nexus/content/groups/public"}
	}

	dependencies {
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.3.1.3277"
	}
}

plugins {
	id 'distribution'
}

subprojects { subproject ->
	apply plugin: "io.spring.dependency-management"
	plugins.withId("java") {
		sourceCompatibility = "11"
	}

	buildscript {
		repositories {
			maven {
				url "https://repository-cdn.liferay.com/nexus/content/groups/public"
			}
		}
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}

	dependencyManagement {
		dependencies {
			dependency "com.liferay.portal:release.dxp.api:7.4.13.u91"
			dependency "javax.mail:mail:1.4.7"
		}
	}
}

apply plugin: "com.liferay.node"

node {
	nodeVersion = "16.15.1"
}

apply plugin: "org.sonarqube"


def env = System.getenv("enviroment")

distributions {
	main {
		distributionBaseName = 'portalempleado'
		contents {
			into('deploy/') {
				from 'bundles/deploy'
			}
			into('osgi/modules') {
				from 'bundles/osgi/modules'
			}
			into('osgi/configs') {
				from 'configs/common/osgi/configs'
			}
			into('osgi/configs') {
				from 'configs/'+env+'/osgi/configs'
			}
			into('.') {
				from 'configs/common/portal-setup-wizard.properties'
			}
			into('.') {
				from 'configs/'+env+'/portal-emasesa.properties'
			}
			into('tomcat/webapps/ROOT/WEB-INF/shielded-container-lib') {
				from 'shielded-lib'
			}
		}

	}
}